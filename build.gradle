// Check out the changes we've made to build.gradle
// Getting into a fight with Gradle isn't really part of the learning objectives for this course
// so these TODOs are already complete.

// Import com.badlogic.gdx.tools.texturepacker.TexturePacker
import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
    ext.kotlinVersion = '1.2.71'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.mobidevelop.robovm:robovm-gradle-plugin:2.3.5'
        // Add buildscript dependency on gdx-tools
        classpath "com.badlogicgames.gdx:gdx-tools:1.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "GigaGal"
        gdxVersion = '1.9.8'
        roboVMVersion = '2.3.5'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "kotlin"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

project(":android") {
    apply plugin: "android"
    apply plugin: "kotlin-android"

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

project(":ios") {
    apply plugin: "kotlin"
    apply plugin: "robovm"

    dependencies {
        implementation project(":core")
        implementation "com.mobidevelop.robovm:robovm-rt:$roboVMVersion"
        implementation "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

project(":core") {
    apply plugin: "kotlin"

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }

    // Define packTextures task
    task packTextures {
        def inputDir = "$rootDir/core/rawAssets/sprites"
        def outputDir = "$rootDir/android/assets/images"
        inputs.dir inputDir
        outputs.dir outputDir
        doLast {
            delete(outputDir)
            TexturePacker.process(inputDir, outputDir, "gigagal.pack")
        }
    }

    // Add task dependency between compileJava and packTextures
    project.tasks.compileJava.dependsOn packTextures
}

tasks.eclipse.doLast {
    delete ".project"
}